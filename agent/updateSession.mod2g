use dialog.
use dialogflow.
use ingredient_hierarchies.
use patterns.
use recipe_database.
use recipe_selection.
use utils.

order=linear.

% Updates the session with an intent of either user or agent.
% Tries to identify pattern based on (user) intent if current sequence is empty.
module updateSession(Actor, Intent, Params, Txt) {

	if session([ H | T ]) then {
		% CASE 1: Intent INITIATES A NEW SEQUENCE.
		if H = [], matchingPattern(Actor, Intent, PatternId)
			then delete(session([ [] | T])) + insert(session([ [PatternId] | T]))
			+ updateSession(Actor, Intent, Params, Txt).

		% CASE 2: Intent CONTRIBUTES TO THE CURRENT SEQUENCE.
		if expectedIntent(Actor, Intent), session([H | T]), 
			add(H, [Actor, Intent, Params], NewH)
		then delete(session([ H | T])) + insert(session([ NewH | T])).
		
		% CASE 3: Intent INITIATES A SUBSEQUENCE.
		if matchingPattern(Actor, Intent, PatternId), add(H, [PatternId], NewH)
			then delete(session([H | T])) + insert(session([NewH | T])) % expand session with PatternId 
			+ updateSession(Actor, Intent, Params, Txt). % and add intent (CASE 2)
			
		% CASE 4a: Same intent with same params detected as previous one, it looks like the
		% user is repeating themselves.
% TODO: Needs more work
%		if lastAddedIntentTriple([A, I, P]), Actor = user, Actor=A, Intent=I, Params=P,
%			add(H, [b14], NewH)
%		then delete(session([H | T])) + insert(session([NewH | T])) % expand session with b14 
%			+ updateSession(Actor, Intent, Params, Txt).
						
		% CASE 4b: Agent indicates it recognised text but does not understand the dialog move.
		% Note that an agent never triggers a b13 pattern and the b13 pattern will accept any user
		% intent, so case 2 should be applicable now, and no new recursive call should be triggered.
		if add(H, [b13], NewH)
			then delete(session([H | T])) + insert(session([NewH | T])) % expand session with b13 
			+ updateSession(Actor, Intent, Params, Txt).
	}
}